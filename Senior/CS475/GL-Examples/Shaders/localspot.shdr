///VERT
#version 410

precision highp float;

layout(location=0) in vec3 position;
layout(location=1) in vec3 normal;
layout(location=2) in vec2 uv;

uniform mat4 modelview;
uniform mat4 proj;

out vec3 fragpos;
out vec3 fragnorm;
out vec2 fraguv;

void main(void) {
  fragpos = position;
  fragnorm = normal;
  fraguv = uv;

  gl_Position = proj * modelview * vec4(position, 1.0);
}


///FRAG
#version 410

precision highp float;

in vec3 fragpos;
in vec3 fragnorm;
in vec2 fraguv;

uniform vec3 lightpos;
uniform mat4 lightmatrix;
uniform vec3 color;
uniform vec4 material;
uniform bool usespot;
uniform vec3 spotdir;
uniform bool usetex;
uniform sampler2D texid;
uniform mat4 modelview;

out vec3 outcolor;

// assume eye is at origin
void main(void) {
  vec3 transposition = vec3(modelview * vec4(fragpos,1));
  vec3 translightpos = vec3(lightmatrix * vec4(lightpos,1));
  vec3 transnormal = normalize(transpose(inverse(mat3(modelview))) * fragnorm);

  float ambient = material[0];

  vec3 lightdir = normalize(translightpos - transposition);

  float diffcmp = max(0.0, dot(transnormal, lightdir));
  float diffuse = material[1] * diffcmp;

  vec3 reflvec = reflect(-lightdir,transnormal);
  vec3 eyedir = normalize(-transposition);
  float specdot = max(0.0, dot(eyedir,reflvec));
  float speccmp = pow(specdot,material[3]);
  vec3 specclr = vec3(diffuse > 0);
  float specular = material[2] * speccmp;

  vec3 fragcolor = color;
  if (usetex) {
    fragcolor = vec3(texture(texid,vec2(fraguv[0],1-fraguv[1])));
  }

  vec3 transspotdir = normalize(transpose(inverse(mat3(lightmatrix))) * spotdir);
  float spotdot = dot(transspotdir,-lightdir);
  float spotcmp = float(spotdot > 0.8);
  spotcmp = float(usespot) * spotcmp + (1-float(usespot));

  outcolor = clamp(fragcolor * ambient
                   + spotcmp * fragcolor * diffuse
                   + spotcmp * specclr * specular,0,1);
}

///END
